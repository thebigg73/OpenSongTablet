apply plugin: 'com.android.application'
apply plugin: 'androidx.navigation.safeargs'

android {
    compileSdkVersion 33
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        beta {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        resources.excludes.add("META-INF/*")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.garethevans.church.opensongtablet"
        minSdkVersion 19
        targetSdkVersion 33
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        versionCode 335
        versionName '6.0.0_beta10'
    }

    buildscript {
        repositories {
            google()
            mavenCentral()
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:4.2.2'
            classpath 'androidx.navigation:navigation-safe-args-gradle-plugin:2.5.3'
        }
    }

    repositories {
        google()
        mavenCentral()
        maven {
            url "https://jitpack.io"
        }
    }

    buildFeatures {
        viewBinding true
    }

    namespace 'com.garethevans.church.opensongtablet'
}

dependencies {
    implementation 'androidx.activity:activity:1.6.1'                                   // Required for permission requests
    implementation 'androidx.fragment:fragment:1.5.5'                                   // Using fragments and navigation
    implementation 'com.google.android.material:material:1.9.0-beta01'                 // Required for material design goodness
    //implementation "androidx.compose.material:material:1.3.1"                         // Can upgrade theme to Material3 once I remove support for KitKat
    implementation 'com.google.android.gms:play-services-cast:21.2.0'                   // For casting obviously!
    implementation 'com.google.android.gms:play-services-cast-framework:21.2.0'         // For casting obviously!
    implementation 'androidx.mediarouter:mediarouter:1.3.1'                             // For casting obviously!
    implementation 'com.google.android.gms:play-services-nearby:18.5.0'                 // For Google Nearby Connect devices
    implementation 'com.google.android.gms:play-services-mlkit-text-recognition:18.0.2' // For OCR text recognition (PDF/Camera)
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'                 // RecyclerView (obviously)
    implementation 'androidx.recyclerview:recyclerview:1.3.0'                           // RecyclerView (obviously)
    implementation 'androidx.gridlayout:gridlayout:1.0.0'                               // GridLayout (obviously)
    implementation 'androidx.appcompat:appcompat:1.6.1'                                 // Allows access to app: features for older devices
    implementation 'androidx.cardview:cardview:1.0.0'                                   // Cardview for recycler list items
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'                   // Constraint layouts with FAB
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'                      // Lifecycle for fragment control
    implementation 'androidx.navigation:navigation-fragment:2.5.3'                      // The navigation fragment control
    implementation 'androidx.navigation:navigation-ui:2.5.3'                            // The navigation fragment control
    implementation 'androidx.navigation:navigation-dynamic-features-fragment:2.5.3'     // The navigation fragment control
    implementation 'androidx.preference:preference:1.2.0'                               // The app preferences
    implementation 'androidx.multidex:multidex:2.0.1'                                   // Allow splitting of file for older devices
    implementation 'androidx.annotation:annotation:1.6.0'                               // Allows annotations in code
    annotationProcessor 'androidx.annotation:annotation:1.6.0'                          // Allows annotations in code
    implementation 'org.apache.commons:commons-compress:1.22'                           // For creating and editing compressed files (osb, backup)
    implementation files('libs/TarsosDSP-Android-2.4.jar')                              // For the pitch detection in the tuner
    implementation 'com.github.bumptech.glide:glide:4.14.2'                             // For better setting of images
    annotationProcessor 'com.github.bumptech.glide:compiler:4.14.2'                     // For better setting of images
    implementation 'com.github.deano2390:MaterialShowcaseView:1.3.7'                    // To highlight new features in the app

    // To include jar libraries
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Updates to material alpha and other dependencies using kotlin can cause errors
    // if they point to different libraries.
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}
